## . path operator


The dot operator may be used as a short-cut when specifying a
path in the DM code tree. Instead of specifying the full path, you can
start a path with a dot and the compiler will search up in the code tree
for the following node. This is known as a relative path with an
"upward" search. 

Here are the beginnings of a text MUD that
allows you to walk around between rooms using the arrow keys. The links
between rooms are created in this example by referencing the object type
of the destination room. Since there could potentially be a lot of
rooms, they are grouped into sub-classes, and to avoid lengthy type
paths such as /area/Village/Square, they are referenced using a relative
path from the point of reference.
### Example:

```dm
area
    var/area
        north_exit
        south_exit
        east_exit
        west_exit

    Entered(O)
        O << name
        return ..()

    Castle
        Main_Gate
            north_exit = .Castle_Entryway
            south_exit = .Moat_Bridge
        Castle_Entryway
            south_exit = .Main_Gate
        Moat_Bridge
            north_exit = .Main_Gate
            south_exit = .Village/Guard_Post
    Village
        Guard_Post
            north_exit = .Castle/Moat_Bridge
            south_exit = .Square
        Square
            north_exit = .Guard_Post

//handle movement
client/Move(Dest,Dir)
    var/area/room = usr.loc
    if(istype(room)) //in a room
        switch(Dir)
            if(NORTH) Dest = room.north_exit
            if(SOUTH) Dest = room.south_exit
            if(EAST)  Dest = room.east_exit
            if(WEST)  Dest = room.west_exit
    return ..()

//set the starting position for new logins
mob/Login()
    if(!loc) Move(locate(/area/Castle/Main_Gate))
    return ..()
```

The dot operator can also be used to navigate the type tree at compile-time.
One (.) operator will begin at the current compile-time path node.
Two (..) will specify the parent of the current path.
Three (...) will specify the grandparent of the current path.
Each further . operator will move one level further up.

```dm
turf
    child1
        .grandchild1 //full path: /turf/child1/grandchild1
    child2
        ..child3 //full path: /turf/child3
        ...datum1 //full path: /datum1
```

You can also separate these with path operators to make them more readable:

```dm
turf
    child1
        ./grandchild1
    child2
        ../child3
        ../../datum1
```

The same is true for referencing paths at compile-time:

```dm
turf
    child1
        var
            child_path = .grandchild1
            sibling_path = ..child2
        grandchild1
	    child2
```

> [!TIP] 
> **See also:**
> +   [/ path operator](/ref/operator/path//.md) 
> +   [: path operator](/ref/operator/path/:.md) 
